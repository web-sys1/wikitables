{
	let lang = window.location.hostname;
	lang = lang.substring(0, lang.indexOf('.'));
	function ciEquals(a, b) { return a.localeCompare(b, lang, {sensitivity:"base"}) === 0; }
}

X = _ x:EXPR _ { return x; }

SQuoted = "'" value:([^']*) "'" { return value.join(''); }
DQuoted = '"' value:([^"]*) '"' { return value.join(''); }

Term = SQuoted / DQuoted

EQ = left:Term _ '==' _ right:Term { return valueOfRowField(left) === right; }
NEQ = left:Term _ '!==' _ right:Term { return valueOfRowField(left) !== right; }
CIEQ = left:Term _ '=' _ right:Term { return ciEquals(valueOfRowField(left), right); }
CINEQ = left:Term _ '!=' _ right:Term { return !ciEquals(valueOfRowField(left), right); }

ATOM
  = EQ
  / NEQ
  / CIEQ
  / CINEQ
  / '(' _ x:EXPR _ ')' { return x; }

NOT
  = 'not' _ x:ATOM { return !x; }
  / ATOM

AND
  = head:NOT tail:(_ 'and' _ NOT)* { return tail.reduce((acc, el) => { return acc && el[3]; }, head); }

OR
  = head:AND tail:(_ 'or' _ AND)* { return tail.reduce((acc, el) => { return acc || el[3]; }, head); }

EXPR
  = OR

_ "whitespace"
  = [ \t\n\r]*
